apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def debugKeyPath = new File(System.properties['user.home'], ".android/debug.keystore")

android {
    signingConfigs {
        fromVariablesOrDebug {
            storeFile new File(getSystemVariableOrDefault("KEYSTORE_FILE", debugKeyPath.path))
            storePassword getSystemVariableOrDefault("KEYSTORE_PASSWORD", "android")
            keyAlias = getSystemVariableOrDefault("KEYSTORE_KEY_NAME", "androiddebugkey")
            keyPassword getSystemVariableOrDefault("KEYSTORE_KEY_PASSWORD", "android")
        }
        release {
            storeFile new File(getSystemVariableOrDefault("KEYSTORE_FILE", ""))
            storePassword getSystemVariableOrDefault("KEYSTORE_PASSWORD", "")
            keyAlias = getSystemVariableOrDefault("KEYSTORE_KEY_NAME", "")
            keyPassword getSystemVariableOrDefault("KEYSTORE_KEY_PASSWORD", "")
        }
    }
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.frostnerd.smokescreen"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 32
        versionName "1.0-Beta"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        kapt {
            arguments {
                arg("room.schemaLocation", "$projectDir/schemas".toString())
            }
        }
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
        vectorDrawables {
            useSupportLibrary = true
        }
    }
    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }
    buildTypes {
        release {
            minifyEnabled true
            debuggable false
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        fdroid {
            initWith release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions "version"
    productFlavors {
        adblocker {
            dimension "version"
            versionNameSuffix "-adblock"
            versionCode 33
        }
        normal {
            dimension "version"
        }
    }
    packagingOptions {
        exclude 'META-INF/library_release.kotlin_module'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions{
        disable 'MissingTranslation'
    }
}

dependencies {
    def room_version = "2.1.0"

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.2.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.2.1'

    implementation 'com.frostnerd.utilskt:preferences:1.5.8'
    implementation 'com.frostnerd.utilskt:navigationdraweractivity:1.3.15'
    implementation 'com.frostnerd.utilskt:encrypteddnstunnelproxy:1.5.102'
    implementation 'com.frostnerd.utilskt:general:1.0.14'
    implementation 'com.frostnerd.utils:materialedittext:1.0.20'
    implementation 'com.frostnerd.utils:design:1.0.17'
    implementation 'com.frostnerd.utilskt:networking:1.0.2'
    implementation 'com.frostnerd.utilskt:adapters:1.0.5'

    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation "androidx.preference:preference:1.0.0"

    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-coroutines:2.1.0-alpha04"
    testImplementation "androidx.room:room-testing:$room_version"

    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0-alpha'

    implementation 'io.sentry:sentry-android:1.7.16'
    implementation 'com.github.anrwatchdog:anrwatchdog:1.4.0'

    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta2'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
